#!/usr/bin/perl
use 5.010;
use strict;
use warnings;
use autodie;
use FindBin qw($Bin);
use JSON::PP;

# Write various version things for ProceduralParts.
# Paul Fenwick, January 2015.
# This code is CC-0 licensed. You can use it for anything, without attribution.

# This requires JSON::PP to run, which comes with Perl since v5.14, or can be
# installed with `cpanm JSON::PP`.

my $VERSION_FILE      = "ProceduralParts.version";
my $ASSEMBLY_TEMPLATE = "Source/Properties/AssemblyInfo.in";
my $ASSEMBLY_FILE     = "Source/Properties/AssemblyInfo.cs";

# Here's our base metadata. We'll add VERSION info to this before outputting.
my $metadata = {
    NAME => "Procedural Parts",
    URL  => "https://raw.githubusercontent.com/Swamp-Ig/ProceduralParts/master/ProceduralParts.version",
	DOWNLOAD => "https://github.com/Swamp-Ig/ProceduralParts/releases",
	GITHUB =>
	{
		USERNAME => "Swamp-Ig",
		REPOSITORY => "ProceduralParts"
	
	},
    KSP_VERSION => {
        MAJOR => 1,
        MINOR => 0,
        PATCH => 4,
    },
};

chdir($Bin);

my $version = `git describe --tags --long --match "v*"`;
chomp($version);

if ($version !~ m{
    # Parse a version in the form v1.2.3-4
    ^v
    (?<major>\d+)\.
    (?<minor>\d+)\.
    (?<patch>\d+)-
    (?<build>\d+)
}x) {
    die "Cannot parse $version as a version!";
}

my $assembly_version = "$+{major}.$+{minor}.$+{patch}.$+{build}";

say "Writing metadata for version $assembly_version";

# Using `int` below forces Perl to internally represent these as numbers,
# even though they were the result of a string operation. This means no
# unsightly quotes in the output JSON.

$metadata->{VERSION} = {
    MAJOR => int $+{major},
    MINOR => int $+{minor},
    PATCH => int $+{patch},
    BUILD => 0              # If used, AVC could try to get the user to
                            # install git builds, which we don't want.
};

# Write our AVC metadata

open(my $version_fh, '>', $VERSION_FILE);
say {$version_fh} JSON::PP->new->pretty->encode( $metadata );
close $version_fh;

# Write our AssemblyInfo

open(my $template_fh, '<', $ASSEMBLY_TEMPLATE);
open(my $assembly_fh, '>', $ASSEMBLY_FILE);

while (<$template_fh>) {
    s{\@VERSION\@}{$assembly_version};
    print {$assembly_fh} $_;
}

close($template_fh);
close($assembly_fh);
